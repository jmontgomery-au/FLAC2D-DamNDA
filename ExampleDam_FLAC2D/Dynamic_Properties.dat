;--------------------------------------------------------
;   Seismic Analysis of a dam - FLAC2D
;
;Store soil properties for dynamic analysis and assign to zones by group
;		
;   Prepared by Jack Montgomery, jmontgomery@auburn.edu
;      October 2024 - Revised May 2025
;--------------------------------------------------------
;
;License information and the latest version of these files can be found at:
;https://github.com/jmontgomery-au/

;--------------------------------------------------------
;  Units: Pa, kg, m, s
;  
;--------------------------------------------------------

;
; Function to assign PM4Sand or PM4Silt to each zone based on group name
; For each group name, set either UsePM4Sand or UsePM4Silt equal to 1 (these functions are defined in Dynamic_Functions.dat)
; PM4Sand needs $N160 and $hpo
; If optional parameters are set, user must specify 
fish define set_PM4Zone(zpnt)
    if zone.model(zpnt) # 'null' then
        local z_group = zone.group(zpnt,"Default") ; Read group name from 'Construction' slot
        local z_id = zone.id(zpnt) ; Store Zone ID
		; Initialize all variables
        local UsePM4Sand = 0
        local UsePM4Silt = 0
        local UseOptParam = 0
		$InitialOptParams
		;
       if string.find(z_group,"Shell") # 0 then ;Group is Shell
            $Dr 	= 0.75		; Relative density
            $Go   	= 906		; Shear modulus coefficent
            $hpo    = 0.62		; Contraction rate parameter
            UsePM4Sand = 1
        endif
        if string.find(z_group,"Alluvium") # 0 then ;Group is Alluvium 
            $Dr 	= 0.55		; Relative density
            $Go   	= 677		; Shear modulus coefficent
            $hpo    = 0.40		; Contraction rate parameter
            UsePM4Sand = 1
        endif
        if string.find(z_group,"Berm") # 0 then ;Group is Berm
            $Dr 	= 0.75		; Relative density
            $Go   	= 906		; Shear modulus coefficent
            $hpo    = 0.62		; Contraction rate parameter
            UsePM4Sand = 1
        endif
        if string.find(z_group,"Core") # 0 then ;Group is Core
            $Go          = 500   ; Shear modulus coefficent
            $SuRat        = 0.25    ;Undrained shear strength at critical state for earthquake conditions
            $hpo         = 20.  ; Contraction rate parameter
            UsePM4Silt = 1
        endif
        if UsePM4Sand = 1 then
            ;$Dr 	= math.sqrt($N160/46.0)		; Field: Estimate from SPT or CPT data
            ;$Go    	= 167.*math.sqrt($N160+2.5)		; Field: Calibrate to Vs data
            $Assign_PM4Sand(z_id) ; Assign PM4Sand and properties to current zone
			if UseOptParam = 1 then
                $Assign_PM4Sand_Optional(z_id)
            endif
        endif
        if UsePM4Silt = 1 then
            $Assign_PM4Silt(z_id) ; Assign PM4Sand and properties to current zone
            if UseOptParam = 1 then
                $Assign_PM4Silt_Optional(z_id)
            endif
        endif
    endif
end